/*
作者:集英
日期:20210906
目的:
取出擴充事件中特定項目內容，可指定一個log檔案或者全部
*/

declare @xelpath nvarchar(200) 
declare @xelpathall nvarchar(200) 

/*這個參數是取最新的那個參數檔*/
select   @xelpath = cast(t.target_data as xml).value(N'(EventFileTarget/File/@name)[1]', N'nvarchar(200)') 
from sys.dm_xe_sessions s
inner join sys.dm_xe_session_targets t on s.address = t.event_session_address
where s.name = 'XTN_Maintenance_Base' 

/*這個參數是取所有參數檔*/
select   @xelpathall = 'C:\Program Files\Microsoft SQL Server\MSSQL12.MSSQLSERVER\MSSQL\Log\XTN_Maintenance_Base_*.xel'

/*
--這個是可以查出檔案路徑
select    cast(t.target_data as xml).value(N'(EventFileTarget/File/@name)[1]', N'nvarchar(200)') 
from sys.dm_xe_sessions s
inner join sys.dm_xe_session_targets t on s.address = t.event_session_address
where s.name = 'XTN_Maintenance_Base' 
*/

select *
from (
	select  xml_event_data.value(N'(event/@timestamp)[1]', N'datetime') as date_time
	,xml_event_data.value(N'(event/action[@name="client_hostname"]/value)[1]', N'nvarchar(20)') as client_hostname
	,xml_event_data.value(N'(event/action[@name="client_app_name"]/value)[1]', N'nvarchar(60)') as client_app_name
	,xml_event_data.value(N'(event/action[@name="server_instance_name"]/value)[1]', N'nvarchar(20)') as server_instance_name
	,xml_event_data.value(N'(event/data[@name="message"]/value)[1]', N'nvarchar(max)') as [message]
	from (
		select cast(event_data as xml) as xml_event_data
		from sys.fn_xe_file_target_read_file
        /*修改這處的參數可轉換為查詢全部或者單檔*/
		(@xelpathall, null, null, null)
		where object_name = 'error_reported' 
	) m
)t
where date_time > '20210906'

